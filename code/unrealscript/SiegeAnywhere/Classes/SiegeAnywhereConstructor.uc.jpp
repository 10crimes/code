// Modifies the default sgConstructor, so that it will refuse to build anywhere nearby an important Actor.
// This should prevent people from blocking objectives in Assault, or building things right next to flagspots in CTF.

class SiegeAnywhereConstructor extends sgConstructor config (SiegeAnywhere);

//// This worked once we made it simulated.
#define SIEGEANYWHERE_ADD_TO_CONSTRUCTOR

var config bool bBlockBuildingOnKeypoints;
var config float HealthBasedRUPerSec;

defaultproperties {
	bBlockBuildingOnKeypoints=False // This is good for sensible games, but annoying for players.  Let admin turn it on.
	HealthBasedRUPerSec=1.0 // For non Siege games, how much RU to give a player each second (if they have 100 health).
	#ifndef SIEGEANYWHERE_ADD_TO_CONSTRUCTOR
	BuildCategories(2)=class'SiegeAnywhereCategoryItems'
	BuildCategories(3)=class'SiegeAnywhereCategoryTeleport'
	#endif
}
// TODO: Instead of overwriting the BuildCategories here, make a function which
// checks to see if the items are available, and appends them to the
// appropriate list if they are.
// This could work live on any siege gametype, and be directed by a config file!

function PlaceIt()
{
    local sgPRI     ownerPRI;
    local class<sgBuilding>
                    type;
	local sgBuilding
                    test;
	local Vector TargetLocation;
	local sgCameraView
					stest;
	local int i;
	local float amount;


    if ( Pawn(Owner) == None || sgPRI(Pawn(Owner).PlayerReplicationInfo) ==
      None)
        return;
    ownerPRI = sgPRI(Pawn(Owner).PlayerReplicationInfo);

    type = GetBuildType();

    if ( type == None )
        return;

/*
	if (type==class'sgFactory')
		if (!CanIMakeMoreFactoriesPlease())
		{
			Owner.PlaySound(Misc1Sound, SLOT_Misc, Pawn(Owner).SoundDampening*2.5);
			Pawn(Owner).ClientMessage("Build Message: one Factory allowed per team once a Super Supplier is built.");
			return;
		}
*/
	if (type==class'sgTeleporter')
		if (!CanIMakeMoreTeleportersPlease())
		{
			Owner.PlaySound(Misc1Sound, SLOT_Misc, Pawn(Owner).SoundDampening*2.5);
			Pawn(Owner).ClientMessage("Build Message: only one pair of Teleporters allowed per player.");
			return;
		}
	if (type==class'sgPlatform')
		if (!CanIMakeMorePlatformsPlease())
		{
			Owner.PlaySound(Misc1Sound, SLOT_Misc, Pawn(Owner).SoundDampening*2.5);
			Pawn(Owner).ClientMessage("Build Message: only one Platform allowed per player.");
			return;
		}

	if (type==class'sgHomingBeacon')
		if (!CanIMakeMoreBeaconsPlease())
		{
			Owner.PlaySound(Misc1Sound, SLOT_Misc, Pawn(Owner).SoundDampening*2.5);
			Pawn(Owner).ClientMessage("Build Message: only one Homing Beacon allowed per player.");
			return;
		}

	if (!(SiegeGI(Level.Game) != None && SiegeGI(Level.Game).FreeBuild)
      && ownerPRI.RU < type.default.BuildCost )
	{
		Owner.PlaySound(Misc1Sound, SLOT_Misc, Pawn(Owner).SoundDampening*2.5);
		Pawn(Owner).ClientMessage("Build Message: You do not have sufficient RU to build this.");
		return;
	}

	if (type==class'sgCameraView')
	{
		CreateScreen(OwnerPRI);
		return;
	}

	TargetLocation = Owner.Location - vect(0,0,8) +
		vector(Pawn(Owner).ViewRotation) * 45;

	if (bBlockBuildingOnKeypoints && ShouldNotBuild(type,TargetLocation,Pawn(Owner))) {
		test = None;
	} else {
		test = Spawn(type, Owner,, TargetLocation, Pawn(Owner).ViewRotation);
	}

	if ( test != None)
	{

		for (i=0;i<NumBuildings;i++)
		{
			if (test.BuildingName == right(sgBuildings[i],len(sgBuildings[i])-1))
			{
				sgGameReplicationInfo(SiegeGI(Level.Game).GameReplicationInfo).sgTotalBuilt[i]++;
				ownerPRI.sgBuilt[i]++;
			}
		}


		Owner.PlaySound(FireSound, SLOT_None, Pawn(Owner).SoundDampening*2.5);
        	if ( SiegeGI(Level.Game) == None || !SiegeGI(Level.Game).FreeBuild )
			ownerPRI.RU -= type.default.BuildCost;

		ownerPRI.Score += type.default.BuildCost / 100;
		ownerPRI.sgInfoBuildingMaker++;
	}
	else
        Owner.PlaySound(Misc1Sound, SLOT_Misc, Pawn(Owner).SoundDampening*2.5);
}

function bool ShouldNotBuild(class type, Vector Location, optional Pawn Owner) {
	local Actor p;
	local String OwnerName;
	foreach AllActors(class'Actor',p) {
		if (
				!
				( // these actors can never block:
					p.IsA('PlayerPawn')
					|| p.IsA('Bot')
					|| p.IsA('PathNode')
					|| p.IsA('SoundEvent')
					|| p.IsA('Light')
					|| p.IsA('locationid')
					|| p.IsA('sgBuilding') // presumably dealt with elsewhere
					|| (p.IsA('Weapon') && (Owner==p.Owner || Weapon(p).bHeldItem)) // my weapon, or a held weapon
					|| p.IsA('Projectile')
					|| p.IsA('Mover') // TODO CONSIDER: the jury is still out on this one
					|| p.IsA('Decal')
					|| p.IsA('Decoration')
					|| p.IsA('Effects') // NOT Effect - it's Effects!
					|| p.IsA('RocketTrail')
					|| p.IsA('DefensePoint')
				)
			&&
				( // these actors always block building when near:
					true // So we can collect the names of relevant actors.  Add ones we wish to whitelist above.
					|| p.IsA('Trigger')
					|| p.IsA('FlagBase')
					// || p.IsA('CTFFlag')
					|| p.IsA('FortStandard')
					|| p.IsA('PlayerStart')
					|| p.IsA('InventorySpot')
					|| (p.IsA('Weapon') && Owner!=p.Owner && !Weapon(p).bHeldItem) // pickup point, and not my weapon, or held by another
					|| (p.IsA('Inventory') && Owner!=p.Owner && !Inventory(p).bHeldItem) // pickup point, and not my weapon, or held by another
					|| (p.IsA('Pickup') && Owner!=p.Owner && !Pickup(p).bHeldItem) // pickup point, and not my weapon, or held by another
				)
			&&
				VSize(p.Location - Location) < 96
				// 128
				// 96 seemed a little too close to me; mines could still reach a player grabbing the flag.
				// I wouldn't mind expanding it even more.  Although it could end up blocking too much of the map.
		) {
			// if (p.IsA('Weapon') && (p.Owner == Owner) || Weapon(p).bHeld)
				// continue; // Somehow this was not caught above.
			OwnerName = p.Owner.getHumanName();
			if (OwnerName == "")
				OwnerName = String(p.Owner);
			if (OwnerName == "" || OwnerName == "None")
				OwnerName = "The";
			else
				OwnerName = OwnerName $ "'s";
			// Owner.ClientMessage("The " $ p.class $ " blocks you from placing a " $ type $ " here.");
			// Owner.ClientMessage(OwnerName $ " " $ p.class.name $ " blocks you from placing a " $ type $ " here.");
			Owner.ClientMessage(OwnerName $ " " $ p.class.name $ " blocks you from building here.");
			// p.bvisible=true; p.drawscale=1.0+0.5*(FRand());
			return true;
		}
	}
	return false;
}



function float RateSelf( out int bUseAltMode ) {
	if (bUseAltMode>0)
		return -0.2;
	return -0.4; // Bots go crazy in SiegeAnywhere, shooting at teammates etc., so it's better if they use the Constructor!
}

function Pawn GetTarget() {
	if (Bot(Owner) != None) {
		// Prevents Accessed Nones being thrown.
		return None;
	} else {
		// Needed for player's Upgrade to work (maybe everything else too).
		return Super.GetTarget();
	}
}

simulated function PostBeginPlay() {
	SetTimer(1,True);
	Super.PostBeginPlay();

	#define SIEGEANYWHERE_ADD_TO_CONSTRUCTOR
	AddExtraBuildOptions();
	#endif
}

event Timer() {
	local PlayerPawn p;
	local sgPRI pri;
	p = PlayerPawn(Owner);
	if (p == None)
		return;
	pri = sgPRI(p.PlayerReplicationInfo);
	if (pri == None)
		return;
	// TODO: Does this do anything if it comes out as an int less than 1?
	if (HealthBasedRUPerSec > 0 && SiegeGI(Level.Game)==None) {
		pri.AddRU( HealthBasedRUPerSec * p.Health / 100.0 );
		if (pri.RU > pri.MaxRU) {
			pri.MaxRU = (pri.MaxRU + pri.RU) / 2; // grow max at half the speed
			pri.RU = pri.MaxRU;
		}
	}
}

#ifdef SIEGEANYWHERE_ADD_TO_CONSTRUCTOR

simulated function AddExtraBuildOptions() {
	//// TODO: Remove Invisibility.  It sucks for sgCTF and sgAS.
	//// TODO: Remove Jetpack from sgAS, and from sgCTF until we can force flagdrop when jetting.
	// if ( ! sgCategoryTeleport.class.default.Selections[4] == class'sgItemGrappleGun' ) {
		// InsertInCategory(class'sgCategoryTeleport',class'sgItemGrappleGun',4);
	// }
	InsertInCategory(class'sgCategoryItems',class'sgItemDoubleJumpBoots',2);
	// InsertInCategory(class'sgCategoryItems',class'sgItemGhost',3);
	InsertInCategory(class'sgCategoryItems',class'sgItemForceGun',5);
	// InsertInCategory(class'sgCategoryItems',class'sgItemSpawner',6); // not an item, just a building!  it doesn't display properly in constructor menu ... maybe if we set its defaults?  it doesn't work anyway :P
	InsertInCategory(class'sgCategoryTeleport',class'sgItemGrappleGun',4);
	InsertInCategory(class'sgCategoryExplosives',class'sgItemVoiceBox',5);
}

simulated function InsertInCategory(class<sgBuildCategory> categoryClass, class<sgBuilding> BuildClass, int pos) {
	local int i;
	if (categoryClass.default.Selections[pos] == BuildClass)
		return; // It's already there where we wanted to put it - do nothing.
	for (i=categoryClass.default.NumSelections;i>pos;i--) {
		categoryClass.default.Selections[i] = categoryClass.default.Selections[i-1];
	}
	categoryClass.default.Selections[pos] = BuildClass;
	categoryClass.default.NumSelections++;
}

// Example config:
// ExtraItems(1)=ExtraItem(Name="A Bucket",Mesh="BucketMesh",InventoryType="Botpack.Bucket",BuildCost=250,UpgradeCost=50)
// Mmmm unfortunately the current system requires that a separate sgItem class exists for each item.

#endif

