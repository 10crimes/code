class SiegeForever extends Mutator config(SiegeForever);

// BUG: If only 1 player is present at game start, all auto-loaded buildings will belong to him, and have an inner glow of his colour.
// DONE: TryToLoadBuildings() should use random players as owners, if more than 1 are available.
// DONE: Detect EndGame and auto-save then.  (Well, sorta detected ;p )
// DONE: When a new Type or Map overwrites an old one, we should really clear all relevant recorded building locations for that type/map!
// DONE: If the gametype is Assault, then red/blue is not enough - we end up with buildings swapping side!  We should say 0=Attackers and 1=Defenders, and map from that to/from Red/Blue, according to Team Assault(Level.Game).Attacker and Defender.
// CONSIDER: Could be really clever, and detect whether the map is symmetrical, using some distance-minimisation search on the PlayerStarts, and if so, spawn exactly the same sets of buildings for both teams.
// DONE: do not load or save sgBaseCore!

#define MaxTeams 4
#define MaxMaps 48
#define MaxTypes 20
#define MAX_ALL 3840
// MAX_ALL must be the multiple of the other 3.

var config bool bAutoLoad,bSaveAtEnd,bAutoSave,bRememberOld,bSpawnFairly;

var config String Maps[MaxMaps]; // 0-MaxMaps
var config String Types[MaxTypes]; // 0-MaxTypes
var config String BuildingsData[MAX_ALL]; // 0-(MaxTeams*MaxTypes*MaxMaps)

var bool bDoneLoad;

// var String SavedTypes;
// var String LoadedTypes;

defaultproperties {
	// MaxMaps=16
	// MaxTypes=8
	bAutoLoad=True       // When set, stored buildings will be loaded at the start of the game.  (Otherwise only accessible through "mutate load".)
	bSaveAtEnd=True      // When set, at the end of the game the remaining buildings will be saved.  (May cause lag at 0:00 if game goes into overtime.)
	bAutoSave=False      // When set, buildings in the game will be saved at regular intervals (CAUSES LAG!).
	bRememberOld=False   // When set, will keep records of old buildings which have been destroyed.  (Will only remember buildings destroyed mid-game if bAutoSave=True.)
	bSpawnFairly=True    // When set, on loading buildings, will only spawn an equal number of buildings of each type for each team.
}

function PostBeginPlay() {
	Super.PostBeginPlay();
	SetTimer(20,True);
}

event Timer() {
	if (!bDoneLoad) {
		TryToLoadBuildings();
		return;
	}
	if (bDoneLoad && bAutoSave)
		SaveBuildings();
}

function Mutate(String str, PlayerPawn Sender) {
	if (Sender.bAdmin) {
		// You do not need to be an admin to save the buildings:
		if (str ~= "SAVE" || str ~= "SAVEBUILDINGS") {
			SaveBuildings();
		}
		// Only an admin can run these commands, or any player in the first 10 seconds of the game.
		if (Sender.bAdmin || Level.TimeSeconds < 10) {
			if (str ~= "LOAD" || str ~= "LOADBUILDINGS") {
				TryToLoadBuildings();
				BroadcastMessage(Sender.getHumanName()$" has loaded some buildings.  Type mutate save to save them..");
			}
			if (str ~= "CLEAR" || str ~= "CLEARBUILDINGS" || str ~= "KILLBUILDINGS" || str ~= "RESET") {
				KillAllBuildings(); // Admin could also try: admin killall sgBuilding
				// BroadcastMessage(Sender.getHumanName()" cleared all the buildings.");
				BroadcastMessage(Sender.getHumanName()$" cleared all the buildings.  Type mutate load to re-load them.");
			}
		}
	}
	Super.Mutate(str,Sender);
}

function TryToLoadBuildings() {
	local Pawn redOwner,blueOwner;
	local Pawn p;
	redOwner = None;
	blueOwner = None;
	// We must find a PlayerPawn for every building.
	// In the end, I don't care if he's on the wrong team!
	for (p=Level.PawnList;p!=None;p=p.NextPawn) {
		// if (p.IsA('PlayerPawn') || p.IsA('Bot')) {
		if (p.IsA('PlayerPawn') && p.PlayerReplicationInfo != None) {
			if (p.PlayerReplicationInfo.Team == 0 || redOwner == None) {
				redOwner = p;
			}
			if (p.PlayerReplicationInfo.Team == 1 || blueOwner == None) {
				blueOwner = p;
			}
		}
	}
	if (redOwner != None && blueOwner != None) {
		if (bAutoLoad) {
			LoadBuildings(redOwner,blueOwner);
			BroadcastMessage("Buildings were auto-loaded.  Type mutate clear to remove them.");
		}
		bDoneLoad=True;
		SetTimer(60,True);
	}
}

function KillAllBuildings() {
	local sgBuilding b;
	foreach AllActors(class'sgBuilding',b) {
		b.Destroy();
	}
}

function TrimRecord(int j) {
	local int i;
	if (len(BuildingsData[j]) > 36*32) {
		// BuildingsData[j] = Right(BuildingsData[j],36*32);
		i = InstrOff(BuildingsData[j],":",len(BuildingsData[j])/4);
		BuildingsData[j] = Mid(BuildingsData[j],i+1);
	}
}

function Pawn GetRandomPlayer(int team) {
	local Pawn p;
	local int count;
	count = 0;
	for (p=Level.PawnList;p!=None;p=p.NextPawn) {
		if (p.IsA('PlayerPawn') && p.PlayerReplicationInfo != None && p.PlayerReplicationInfo.Team == team) {
			count++;
		}
	}
	count = count * FRand() + 1;
	for (p=Level.PawnList;p!=None;p=p.NextPawn) {
		if (p.IsA('PlayerPawn') && p.PlayerReplicationInfo != None && p.PlayerReplicationInfo.Team == team) {
			count--;
			if (count == 0)
				return p;
		}
	}
	return None;
}

function LoadBuildings(Pawn redOwner, Pawn blueOwner) {
	local int i,j,k,l,team;
	local string type,buildingLocation,map;
	local class<sgBuilding> typeClass;
	local int countStored[MaxTeams];
	local int maxToSpawn;
	local int countSpawned[MaxTeams];
	// local float chanceOfSpawning;
	local int numToSkip;
	local Actor sgB;
	local Pawn pOwner;
	map = GetMap();
	// local string LoadedTypes;
	for (i=0;i<MaxTypes;i++) {
		type = Types[i];
		if (type == "")
			break;
		// typeClass = Class(type);
		// typeClass = class<sgBuilding>(DynamicLoadObject("SiegeXXL2dRC1.sgBuilding", class'Class'));
		typeClass = class<sgBuilding>(DynamicLoadObject(type, class'Class'));
		// Count how many buildings of this type each team has stored:
		maxToSpawn = 9999;
		for (team=0;team<MaxTeams;team++) {
			j = GetBuildingsI(map,type,team);
			countStored[team] = 0;
			k = 0;
			while (true) {
				l = InstrOff(BuildingsData[j],":",k);
				if (l>-1) {
					countStored[team]++;
					k = l+1;
				} else {
					break;
				}
			}
			if (countStored[team] < maxToSpawn)
				maxToSpawn = countStored[team];
		}
		// Go ahead and spawn buildings of this type:
		for (team=0;team<MaxTeams;team++) {
			if (bSpawnFairly)
				numToSkip = countStored[team] - maxToSpawn;
			countSpawned[team]=0;
			j = GetBuildingsI(map,type,team);
			TrimRecord(j);
			Log("SiegeForever.LoadBuildings(): "$type$" "$team$" BuildingsData["$j$"] = \""$BuildingsData[j]$"\"");
			k = 0;
			while (true) {
				l = InstrOff(BuildingsData[j],":",k);
				if (l>-1) {
					buildingLocation = Mid(BuildingsData[j],k,l-k);
					if (buildingLocation != "") { // could also check for 2 ","s and 3 "."s
						if (bSpawnFairly && numToSkip>0) {
							numToSkip--;
						} else {
							pOwner = GetRandomPlayer(team);
							if (pOwner == None) { // Might happen if only 1 player is present.
								// redOwner and blueOwner are guaranteed to have values, even if there are no red players present.
								if (team == 0)
									pOwner = redOwner;
								if (team == 1)
									pOwner = blueOwner;
							}
							sgB = Spawn(typeClass,pOwner,,Vector(buildingLocation));
							if (sgB == None) {
								Log("SiegeForever.LoadBuildings(): Failed to spawn "$type$" "$typeClass$" at ("$buildingLocation$")");
							} else {
								sgBuilding(sgB).Team = team;
								sgBuilding(sgB).Grade = 2;
								if (FRand() < 0.5)
									sgBuilding(sgB).Grade = 3;
								// sgBuilding(sgB).BuildTime = FRand()*10.0;
								// sgBuilding(sgB).Energy = 75.0;
								sgBuilding(sgB).Energy = sgBuilding(sgB).MaxEnergy;
								// sgBuilding(sgB).Health = 75.0;
								// sgBuilding(sgB).DoneBuilding = True;
								// Log("SiegeForever.LoadBuildings(): Spawned "$sgB$" ("$Pawn(sgB.Owner).PlayerReplicationInfo.Team$")");
								countSpawned[team]++;
							}
						} // else don't spawn it, for fairness!
					}
					k = l+1;
				} else {
					break;
				}
			}
		}
		if (countSpawned[0]>0 || countSpawned[1]>0) {
			BroadcastMessage("Spawned "$countSpawned[0]$" x "$StrAfterLast(type,".")$" for Red, and "$countSpawned[1]$" for Blue.");
			Log("Spawned "$countSpawned[0]$" "$type$"s for Red, and "$countSpawned[1]$" for Blue.");
		}
		// LoadedTypes = LoadedTypes $ ":" $ type $ ":";
	}
}

function SaveBuildings() {
	local int i,TypeI,team,count;
	local string SavedTypes,type,map;
	local sgBuilding A;
	map = GetMap();
	// i = FRand()*MaxRecords;
	SavedTypes="";
	TypeI=-1; // just for the logging at the end
	foreach AllActors(class'sgBuilding',A) {
		if (A.IsA('sgBaseCore')) // Buildings we don't save
			continue;
		type = String(A.class);
		TypeI = GetTypeI(type);
		if (TypeI==-1) {
			continue; // For some reason we couldn't get a type index for this building.  (Never actually happens!)
		}
		if (Instr(SavedTypes,":"$type$":")==-1) {
			// New type:
			SavedTypes = SavedTypes $ ":"$type$":";
			for (team=0;team<MaxTeams;team++) {
				i = GetBuildingsI(map,type,team);
				if (!bRememberOld)
					BuildingsData[i] = "";
				foreach AllActors(class'sgBuilding',A) {
					// if (String(A.class) == type && Pawn(A.Owner).PlayerReplicationInfo.Team == team) {
					if (String(A.class) == type && A.Team == team) {
						if (Instr(BuildingsData[i],A.Location $ ":") == -1) {
							TrimRecord(i);
							BuildingsData[i] = BuildingsData[i] $ A.Location $ ":";
							count++;
						}
					}
				}
			}
		}
	}
	SaveConfig();
	// I'm not sure (TypeI+1) is a good estimate, probably CountOfStr(SavedTypes,":")/2 is better ^^
	BroadcastMessage("Saved "$(TypeI+1)$" types of building, "$count$" new.");
	Log("Saved "$(TypeI+1)$" types of building, "$count$" new.");
}

function int GetBuildingsI(String map, String type, int team) {
	local int i,j,k;
	local String key;
	team = FixTeamForAssault(team);
	i = (team % 2); // 0 or 1
	j = (GetTypeI(type) % MaxTypes);
	k = (GetMapI(map) % MaxMaps);
	// key = map $ ":" $ type $ ":" $ team $ ":";
	// return i + (j * 2) + (k * MaxTypes*2);
	return i + j*2 + k*2*MaxTypes;
}

function int GetMapI(String map) {
	local int i;
	local int type,team;
	for (i=0;i<MaxMaps;i++) {
		if (Maps[i] ~= map) {
			return i;
		}
		// Empty slot:
		if (Maps[i] ~= "") {
			break;
		}
	}
	if (i==MaxMaps) {
		i = MaxMaps*FRand();
		BroadcastMessage("SiegeForever: Ran out of slots for maps!  "$Maps[i]$" will be replaced by "$map);
		Log("SiegeForever: Ran out of slots for maps!  "$Maps[i]$" will be replaced by "$map);
	}
	Maps[i] = map;
	Log("Clearing all data for map "$i$" ("$map$")");
	for (type=0;type<MaxTypes;type++)
		for (team=0;team<MaxTeams;team++)
			BuildingsData[ GetBuildingsI(map,Types[type],team) ] = "";
	return i;
}

function int GetTypeI(String type) {
	local int i;
	local int map,team;
	for (i=0;i<MaxTypes;i++) {
		if (Types[i] ~= type) {
			return i;
		}
		// Empty slot:
		if (Types[i] ~= "") {
			break;
		}
	}
	if (i==MaxTypes) {
		i = MaxTypes*FRand();
		BroadcastMessage("SiegeForever: Ran out of slots for types!  "$Types[i]$" will be replaced by "$type);
		Log("SiegeForever: Ran out of slots for types!  "$Types[i]$" will be replaced by "$type);
	}
	Types[i] = type;
	Log("Clearing all data for type "$i$" ("$type$")");
	for (map=0;map<MaxMaps;map++)
		for (team=0;team<MaxTeams;team++)
			BuildingsData[ GetBuildingsI(Maps[map],type,team) ] = "";
	return i;
}

function String GetMap() {
	local int i;
	i = Instr(""$Self,".");
	return Left(""$Self,i);
}

function int FixTeamForAssault(int team) {
	if (!Level.Game.IsA('Assault')) {
		return team;
	} else {
		if (Assault(Level.Game).Attacker.TeamIndex == 0)
			return team;
		else
			return 1 - team;
	}
}

function bool HandleEndGame() {
	if (bSaveAtEnd)
		SaveBuildings();
	return Super.HandleEndGame();
}

#include "../../JLib/jlib.uc.jpp"

